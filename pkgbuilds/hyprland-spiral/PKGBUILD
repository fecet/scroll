# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: ThatOneCalculator <kainoa@t1c.dev>
# Contributor: Brenno Lemos <brenno@syndel.is>
# Contributor: Gabriel Fox <inbox@gabrielfox.dev>

pkgname=hyprland-spiral
pkgver=0.51.0
pkgrel=1
pkgdesc="A dynamic tiling Wayland compositor with spiral layout support"
arch=(x86_64 aarch64)
url="https://github.com/fecet/hyprspiral"
license=(BSD-3-Clause)
depends=(cairo # libcairo.so
  aquamarine libaquamarine.so
  gcc-libs # libgcc_s.so libstdc++.so
  glibc    # libc.so libm.so
  glib2 libgio-2.0.so libgobject-2.0.so
  glslang
  hyprcursor libhyprcursor.so
  hyprgraphics libhyprgraphics.so
  hyprland-qtutils
  hyprlang libhyprlang.so
  hyprutils libhyprutils.so
  hyprwayland-scanner
  libdisplay-info libdisplay-info.so
  libdrm # libdrm.so
  libglvnd libEGL.so libGLESv2.so libOpenGL.so
  libinput # libinput.so
  libliftoff libliftoff.so
  libx11
  libxcb        # libxcb-dri3.so libxcb-present.so libxcb-render.so libxcb-res.so libxcb-shm.so libxcb.so libxcb-xfixes.so libxcb-xinput.so
  libxcomposite # libxcb-composite.so
  libxcursor    # libXcursor.so
  libxfixes
  libxkbcommon libxkbcommon.so
  libxrender
  mesa # libgbm.so
  opengl-driver
  pango libpango-1.0.so libpangocairo-1.0.so
  pixman libpixman-1.so
  re2 libre2.so
  seatd libseat.so
  systemd-libs libsystemd.so
  tomlplusplus libtomlplusplus.so libudev.so
  util-linux-libs libuuid.so
  wayland libwayland-client.so libwayland-server.so
  wayland-protocols
  xcb-proto
  xcb-util
  xcb-util-errors # libxcb-errors.so
  xcb-util-image
  xcb-util-keysyms
  xcb-util-renderutil # libxcb-render-util.so
  xcb-util-wm         # libxcb-ewmh.so  libxcb-icccm.so
  xorg-xwayland)
makedepends=(cmake
  cpio
  glaze
  hyprland-protocols
  meson
  ninja
  xorgproto)
optdepends=('cmake: to build and install plugins using hyprpm'
  'cpio: to build and install plugins using hyprpm'
  'glaze: to build and install plugins using hyprpm'
  'hyprland-protocols: to build and install plugins using hyprpm'
  'meson: to build and install plugins using hyprpm'
  'uwsm: the recommended way to start Hyprland')
provides=(wayland-compositor "hyprland=${pkgver}")
conflicts=(hyprland)
source=("git+${url}")
sha256sums=('SKIP')

pkgver() {
  cat "$srcdir/hyprspiral/Hyprland/VERSION" | head -n1
}

prepare() {
  cd hyprspiral
  # Hyprland is included via subtree with its submodules already populated
}

build() {
  cd hyprspiral/Hyprland
  make release PREFIX=/usr
}

package() {
  cd hyprspiral/Hyprland
  make DESTDIR="$pkgdir" install
  rm -fv "$pkgdir/usr/include/hyprland/src/version.h.in"
  install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}